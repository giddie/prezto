#
# A colourful, friendly, multiline theme with some handy features.
#
# Authors:
#   Paul Gideon Dann <pdgiddie@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Simple VCS branch, staged, and unstaged indication.
#   - Prompt character is different in a VCS repository.
#   - Last command exit status is displayed when non-zero.
#
# Screenshots:
#   http://i.imgur.com/rCo3S.png
#

# Hooks for vcs_info
function +vi-set_novcs_prompt_symbol() {
  _prompt_giddie_symbol=')'
}
function +vi-set_vcs_prompt_symbol() {
  _prompt_giddie_symbol='Â±'
}
function +vi-git_precmd() {
  # Check for untracked files, since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]+='%F{green}?%f'
  fi
}

function prompt_giddie_preexec {
  _prompt_giddie_timer=$(date +%s%3N)
}

function prompt_giddie_precmd {
  # Replace '/home/<user>' with '~'.
  local prompt_pwd="${PWD/#$HOME/~}"

  # Elapsed time
  if [ $_prompt_giddie_timer ]; then
    local now=$(date +%s%3N)
    local elapsed_time=$(($now-$_prompt_giddie_timer))
    local prompt_elapsed_time="%F{#555}-> ${elapsed_time}ms%f"
    unset _prompt_giddie_timer
  fi

  if [ $prompt_elapsed_time ]; then
    local prompt_return_value='%(?.. %F{red}%B[%?]%b%f)'
    local prompt_prefix_line="${prompt_elapsed_time}${prompt_return_value}
"
  fi

  vcs_info

  PROMPT="${prompt_prefix_line}%F{magenta}%n%f@%F{yellow}%m%f|%F{green}${prompt_pwd}%f\${vcs_info_msg_0_}
%F{blue}\${_prompt_giddie_symbol}%f "
}

function prompt_giddie_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook preexec prompt_giddie_preexec
  add-zsh-hook precmd prompt_giddie_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}--- COMMAND ---%f'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' formats ' on %F{magenta}%b%f%c%u'
  zstyle ':vcs_info:*' actionformats ' on %F{magenta}%b%f%c%u %F{yellow}(%a)%f'
  zstyle ':vcs_info:*' stagedstr '%F{green}+%f'
  zstyle ':vcs_info:*' unstagedstr '%F{green}!%f'

  # Add vcs_info hooks
  # NOTE: Prior to ZSH 4.3.12, there are no static hooks, no vcs_info_hookadd
  #       function, and no 'no-vcs' hook.
  zstyle ':vcs_info:*+start-up:*' hooks set_novcs_prompt_symbol
  zstyle ':vcs_info:git*+set-message:*' hooks set_vcs_prompt_symbol git_precmd
  zstyle ':vcs_info:*+set-message:*' hooks set_vcs_prompt_symbol

  RPROMPT='${editor_info[keymap]}'
  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

prompt_giddie_setup "$@"

# vim: ft=zsh
